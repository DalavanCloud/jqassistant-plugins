apply plugin: 'java'
apply plugin: 'maven'

project.ext["jqaversion"] = "1.0.0-M4"
project.group = 'de.kontext-e.jqassistant.plugin'
project.version = '1.0.0-M6'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: "jacoco"

    //apply plugin: 'pmd'

    project.version = '1.0.0-M6'
    sourceCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.buschmais.jqassistant.core:jqassistant.core.scanner:'+project.jqaversion
        compile 'com.buschmais.jqassistant.core:jqassistant.core.store:'+project.jqaversion
        compile 'com.buschmais.jqassistant.plugin:jqassistant.plugin.common:'+project.jqaversion
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
      classifier = 'javadoc'
      from tasks.javadoc.destinationDir
    }

    task sourcesJar(type: Jar) {
      from sourceSets.main.allSource
      classifier = 'sources'
    }

    artifacts {
      archives jar

      archives javadocJar
      archives sourcesJar
    }


  jacoco {
    toolVersion = "0.7.1.201405082137"
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      csv.enabled false
    }
  }

  checkstyle {
      configFile = new File(rootDir, 'config/checkstyle/checks.xml')
      ignoreFailures = true
      showViolations = false
      toolVersion = '5.7'
    }

    findbugs {
      ignoreFailures = true
      effort = "max"
      reportLevel = "medium"
    }

/*
    pmd {
      ruleSets = ["basic", "braces", "design"]
      ignoreFailures = true
    }
*/

    install {
        repositories.mavenInstaller {
            pom.groupId = 'de.kontext-e.jqassistant.plugin'
        }
    }
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {

  runtime("com.buschmais.jqassistant.core:jqassistant.core.store:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.core:jqassistant.core.scanner:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.core:jqassistant.core.analysis:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.common:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.scm:jqassistant.scm.cli:${project.jqaversion}")

  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.cdi:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.ejb3:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.java:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.java8:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.javaee6:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.jaxrs:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.jpa2:${project.jqaversion}")
  runtime("com.buschmais.jqassistant.plugin:jqassistant.plugin.junit4:${project.jqaversion}")

  runtime("de.kontext-e.jqassistant.plugin:jqassistant.plugin.checkstyle:${project.version}")
  runtime("de.kontext-e.jqassistant.plugin:jqassistant.plugin.findbugs:${project.version}")
  runtime("de.kontext-e.jqassistant.plugin:jqassistant.plugin.jacoco:${project.version}")
  runtime("de.kontext-e.jqassistant.plugin:jqassistant.plugin.git:${project.version}")

  runtime("org.neo4j:neo4j:2.1.5")

  runtime 'com.sun.jersey:jersey-server:1.13'
  runtime 'com.sun.jersey:jersey-core:1.13'
  runtime 'com.sun.jersey:jersey-servlet:1.13'
}

task removeJQAReport(type: Delete) {
  delete 'jqassistant/report'
  delete 'jqassistant/store'
}

task(jqascan, dependsOn: 'removeJQAReport', type: JavaExec) {
  main = 'com.buschmais.jqassistant.scm.cli.Main'
  classpath = sourceSets.main.runtimeClasspath
  args 'scan'
  args '-p'
  args 'jqassistant/jqassistant.properties'
  args '-f'
  args 'checkstyle/build/reports'
  args 'findbugs/build/reports'
  args 'git/build/reports'
  args 'jacoco/build/reports'
  args 'plantuml/build/reports'
}

task(jqaanalyze, type: JavaExec) {
  main = 'com.buschmais.jqassistant.scm.cli.Main'
  classpath = sourceSets.main.runtimeClasspath
  args 'analyze'
  args '-r'
  args 'jqassistant'
}

task(jqa, dependsOn: ['jqascan','jqaanalyze']) {
  jqaanalyze.mustRunAfter jqascan
}

task(jqs, type: JavaExec) {
  main = 'com.buschmais.jqassistant.scm.cli.Main'
  classpath = sourceSets.test.runtimeClasspath
  args 'server'
  standardInput = System.in
}

