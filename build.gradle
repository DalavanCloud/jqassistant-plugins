apply plugin: 'java'
apply plugin: 'maven'

project.ext["jqaversion"] = "1.0.0-M4"
project.group = 'de.kontext-e.jqassistant.plugin'
project.version = '1.0.0-M4'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    //apply plugin: 'pmd'

    version = project.jqaversion
    sourceCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.buschmais.jqassistant.core:jqassistant.core.scanner:'+project.jqaversion
        compile 'com.buschmais.jqassistant.core:jqassistant.core.store:'+project.jqaversion
        compile 'com.buschmais.jqassistant.plugin:jqassistant.plugin.common:'+project.jqaversion
        testCompile group: 'junit', name: 'junit', version: '4.+'
    }


/*
    pmd {

      ruleSets = ["basic", "braces"]
    }
*/

    install {
        repositories.mavenInstaller {
            pom.groupId = 'de.kontext-e.jqassistant.plugin'
        }
    }
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {

  testRuntime("com.buschmais.jqassistant.core:jqassistant.core.store:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.core:jqassistant.core.scanner:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.core:jqassistant.core.analysis:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.common:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.scm:jqassistant.scm.cli:${project.jqaversion}")

  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.cdi:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.ejb3:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.java:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.java8:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.javaee6:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.jaxrs:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.jpa2:${project.jqaversion}")
  testRuntime("com.buschmais.jqassistant.plugin:jqassistant.plugin.junit4:${project.jqaversion}")

  testRuntime("de.kontext-e.jqassistant.plugin:jqassistant.plugin.checkstyle:${project.jqaversion}")
  testRuntime("de.kontext-e.jqassistant.plugin:jqassistant.plugin.findbugs:${project.jqaversion}")
  testRuntime("de.kontext-e.jqassistant.plugin:jqassistant.plugin.jacoco:${project.jqaversion}")
  testRuntime("de.kontext-e.jqassistant.plugin:jqassistant.plugin.git:${project.jqaversion}")

  testRuntime("org.neo4j:neo4j:2.1.5")

  testRuntime 'com.sun.jersey:jersey-server:1.13'
  testRuntime 'com.sun.jersey:jersey-core:1.13'
  testRuntime 'com.sun.jersey:jersey-servlet:1.13'
}

task removeJQAReport(type: Delete) {
  delete 'jqassistant/report'
  delete 'jqassistant/store'
}

task(jqascan, dependsOn: 'removeJQAReport', type: JavaExec) {
  main = 'com.buschmais.jqassistant.scm.cli.Main'
  classpath = sourceSets.test.runtimeClasspath
  args 'scan'
  args '-f'
  args 'git/src/test/properties'
}

task(jqaanalyze, type: JavaExec) {
  main = 'com.buschmais.jqassistant.scm.cli.Main'
  classpath = sourceSets.test.runtimeClasspath
  args 'analyze'
  args '-r'
  args 'jqassistant'
}

task(jqa, dependsOn: ['jqascan','jqaanalyze']) {
  jqaanalyze.mustRunAfter jqascan
}

task(jqs, type: JavaExec) {
  main = 'com.buschmais.jqassistant.scm.cli.Main'
  classpath = sourceSets.test.runtimeClasspath
  args 'server'
  standardInput = System.in
}
